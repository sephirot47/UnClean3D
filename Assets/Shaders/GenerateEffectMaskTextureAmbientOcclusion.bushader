#vertex

uniform mat4 SceneModelMatrix;

layout(location = 0) in vec3 vinTexturePosition;
layout(location = 1) in vec3 vinPosition;
layout(location = 2) in vec3 vinNormal;

out vec3 inWorldPosition;
flat out int inTriId;

void main()
{
    inWorldPosition = (SceneModelMatrix * vec4(vinPosition, 1)).xyz;
    inTriId = gl_VertexID / 3;
    gl_Position = vec4(vinTexturePosition, 1);
}

// ===============================================================

#fragment
#define BANG_FRAGMENT

#include "RayCast.glsl"

uniform int NumRays;
uniform float MaxDistance;

in vec3 inWorldPosition;
flat in int inTriId;

layout(location = 0) out vec4 OutColor;

vec3[3] GetTrianglePoints(int triId)
{
    vec3 triPoints[3];
    for (int vi = 0; vi < 3; ++vi)
    {
        triPoints[vi] = GetTrianglePositionsElement(triId, vi).xyz;
    }
    return triPoints;
}

void main()
{
    vec3 worldPosition = inWorldPosition;
    int triId = inTriId;

    vec3[3] triWorldPosition = GetTrianglePoints(triId);
    vec3 worldNormal = normalize( cross(triWorldPosition[1] - triWorldPosition[0],
                                        triWorldPosition[2] - triWorldPosition[0]) );

    float totalRays = NumRays;
    float maxDistance = MaxDistance;
    float intersectedRaysAO = 0;
    for (int iRay = 0; iRay < totalRays; ++iRay)
    {
        vec3 rayDirection = rand3(worldPosition * (iRay + 100));
        rayDirection = normalize(rayDirection * 2 - 1);
        rayDirection *= sign(dot(rayDirection, worldNormal));

        vec3 rayOrigin = worldPosition + worldNormal * 0.00001f;

        int triId;
        float hitDistance;
        vec3 triBaryCoords;
        if (RayCast(rayOrigin, rayDirection,
                    maxDistance, hitDistance,
                    triId, triBaryCoords))
        {
            float distFactor = (hitDistance / maxDistance);
            intersectedRaysAO += (1.0f - distFactor);
        }
    }

    float ambientOcclusion = (intersectedRaysAO / totalRays);
    OutColor = vec4(vec3(ambientOcclusion), 1);
}


