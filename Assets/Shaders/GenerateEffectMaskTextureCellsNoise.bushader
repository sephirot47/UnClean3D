#vertex

uniform mat4 SceneModelMatrix;

layout(location = 0) in vec3 vinTexturePosition;
layout(location = 1) in vec3 vinPosition;

out vec3 inWorldPosition;
out vec2 inUv;

void main()
{
    inWorldPosition = (SceneModelMatrix * vec4(vinPosition, 1)).xyz;
    inUv = vinTexturePosition.xy * 0.5 + 0.5;
    gl_Position = vec4(vinTexturePosition, 1);
}

// ===============================================================

#fragment
#define BANG_FRAGMENT
#include "Noises.glsl"
#include "Math.glsl"

uniform float Seed;
uniform float Intensity;
uniform float Fade;
uniform float Size;
uniform float Sharpness;

in vec3 inWorldPosition;
in vec2 inUv;

vec2 CellSize = vec2(Size * 0.001);

layout(location = 0) out vec4 OutColor;

vec2 GetClosestPoint(vec2 inPoint)
{
    vec2 cellPos = floor(inPoint / CellSize) * CellSize;
    vec2 r = rand2(cellPos);
    vec2 point = cellPos + r * CellSize;
    return point;
}

void main()
{
    vec2 uv = inUv;
    vec2 firstClosestPoint = vec2(0);
    float firstClosestDist = 99.9f;
    for (int i = -1; i <= 1; ++i)
    {
        for (int j = -1; j <= 1; ++j)
        {
            vec2 ij = vec2(i, j);
            vec2 ijPoint = GetClosestPoint(uv + ij * CellSize);
            float ijDist = distance(uv, ijPoint);
            if (ijDist < firstClosestDist)
            {
                firstClosestPoint = ijPoint;
                firstClosestDist = ijDist;
            }
        }
    }

    float distToFirstPoint = distance(uv, firstClosestPoint);
    distToFirstPoint = pow(distToFirstPoint, 2 * Sharpness);
    distToFirstPoint /= CellSize.x;
    distToFirstPoint *= Intensity * 100;
    distToFirstPoint = clamp(distToFirstPoint, 0, 1);
    OutColor = vec4(vec3(distToFirstPoint), 1);
}


