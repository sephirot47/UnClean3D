#vertex

layout(location = 0) in vec3 vinTexturePosition;
layout(location = 1) in vec3 vinPosition;

out vec3 finPosition;

void main()
{
    finPosition = vinPosition;
    gl_Position = vec4(vinTexturePosition, 1);
}

// ===============================================================

#fragment
#define BANG_FRAGMENT
#include "SimplexNoise3D.glsl"

uniform mat4 SceneModelMatrix;
uniform sampler2D DirtTexture;
uniform float DirtSeed;
uniform float DirtOctaves;
uniform float DirtFrequency;
uniform float DirtFrequencyMultiply;
uniform float DirtAmplitude;
uniform float DirtAmplitudeMultiply;
uniform vec4 DirtOuterColor;
uniform vec4 DirtInnerColor;

in vec3 finPosition;

layout(location = 0) out vec4 OutColor;

void main()
{
    float frequency = DirtFrequency;
    float amplitude = DirtAmplitude;

    float noiseValue = 0.0f;
    for (int i = 0; i < DirtOctaves; ++i)
    {
        vec3 worldPosition = (SceneModelMatrix * vec4(finPosition, 1)).xyz;
        float octaveNoiseValue = snoise(worldPosition * frequency + DirtSeed) * amplitude;
        noiseValue += octaveNoiseValue;

        frequency *= DirtFrequencyMultiply;
        amplitude *= DirtAmplitudeMultiply;
    }

    noiseValue = clamp(noiseValue, 0.01, 1);
    vec3 noise = vec3(noiseValue);

    vec3 dirtOuterColor = DirtOuterColor.rgb;
    vec3 dirtInnerColor = DirtInnerColor.rgb;
    vec3 coloredNoise = mix(dirtOuterColor, dirtInnerColor, noise);

    OutColor = vec4(coloredNoise, noiseValue);
}


