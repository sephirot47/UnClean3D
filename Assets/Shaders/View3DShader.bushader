#properties
#render_pass scene_opaque

// ===============================================================

#vertex
#include "Default.vert"

// ===============================================================

#fragment
#define BANG_FRAGMENT

#include "Common.glsl"
#include "LightCommon.glsl"
#include "MaterialPBRUniforms.glsl"

const int EFFECT_LAYER_BLEND_MODE_ADD      = 0;
const int EFFECT_LAYER_BLEND_MODE_INV_ADD  = 1;
const int EFFECT_LAYER_BLEND_MODE_MULT     = 2;
const int EFFECT_LAYER_BLEND_MODE_INV_MULT = 3;

uniform sampler2D EffectLayerTexture_0;
uniform sampler2D EffectLayerTexture_1;
uniform sampler2D EffectLayerTexture_2;
uniform sampler2D EffectLayerTexture_3;
uniform sampler2D EffectLayerTexture_4;
uniform sampler2D EffectLayerTexture_5;
uniform sampler2D EffectLayerTexture_6;
uniform sampler2D EffectLayerTexture_7;
uniform sampler2D EffectLayerTexture_8;
uniform sampler2D EffectLayerTexture_9;
uniform bool EffectLayerVisibles[10];
uniform int EffectLayerBlendModes[10];
uniform vec4 EffectLayerTints[10];
uniform int NumEffectLayers;

in vec3 B_FIn_Position;
in vec3 B_FIn_Normal;
in vec2 B_FIn_AlbedoUv;
in vec2 B_FIn_NormalMapUv;
in mat3 B_TBN;

layout(location = 0) out vec4 B_GIn_Color;
layout(location = 1) out vec4 B_GIn_Albedo;
layout(location = 2) out vec4 B_GIn_Light;
layout(location = 3) out vec4 B_GIn_Normal;
layout(location = 4) out vec4 B_GIn_Misc;

void DefaultFragCommonMain(vec4 albedoColor)
{
    vec4 texColor = vec4(1);
    if (B_HasAlbedoTexture)
    {
        texColor = texture(B_AlbedoTexture, B_FIn_AlbedoUv);
        if (texColor.a <= B_AlphaCutoff)
        {
            discard;
        }
    }
    vec4 finalAlbedo = albedoColor * texColor;

    vec3 finalNormal = (B_FIn_Normal.xyz);
    if (B_HasNormalMapTexture)
    {
        vec3 normalFromMap = texture(B_NormalMapTexture, B_FIn_NormalMapUv).xyz;
        normalFromMap.xy = (normalFromMap.xy * 2.0f - 1.0f) * B_NormalMapMultiplyFactor;
        normalFromMap = B_TBN * normalFromMap;
        finalNormal = normalFromMap;
    }
    finalNormal = normalize(finalNormal);

    float pixelRoughness = texture(B_RoughnessTexture, B_FIn_AlbedoUv).r * B_MaterialRoughness;
    float pixelMetalness = texture(B_MetalnessTexture, B_FIn_AlbedoUv).r * B_MaterialMetalness;
    vec3 finalPosition = B_FIn_Position.xyz;

    // Apply effect layers
    for (int i = 0; i < NumEffectLayers; ++i)
    {
        if (!EffectLayerVisibles[i])
        {
            continue;
        }

        #define SAMPLE_EFFECT_LAYER(k) texture2D(EffectLayerTexture_##k, B_FIn_AlbedoUv).rgb;

        vec3 effectLayerColor;
        switch (i)
        {
            case 0: effectLayerColor = SAMPLE_EFFECT_LAYER(0); break;
            case 1: effectLayerColor = SAMPLE_EFFECT_LAYER(1); break;
            case 2: effectLayerColor = SAMPLE_EFFECT_LAYER(2); break;
            case 3: effectLayerColor = SAMPLE_EFFECT_LAYER(3); break;
            case 4: effectLayerColor = SAMPLE_EFFECT_LAYER(4); break;
            case 5: effectLayerColor = SAMPLE_EFFECT_LAYER(5); break;
            case 6: effectLayerColor = SAMPLE_EFFECT_LAYER(6); break;
            case 7: effectLayerColor = SAMPLE_EFFECT_LAYER(7); break;
            case 8: effectLayerColor = SAMPLE_EFFECT_LAYER(8); break;
            case 9: effectLayerColor = SAMPLE_EFFECT_LAYER(9); break;
        }

        vec3 tint = EffectLayerTints[i].rgb;
        int effectLayerBlendMode = EffectLayerBlendModes[i];
        switch (effectLayerBlendMode)
        {
            case EFFECT_LAYER_BLEND_MODE_ADD:
            finalAlbedo.rgb += (effectLayerColor * tint);
            break;
            case EFFECT_LAYER_BLEND_MODE_INV_ADD:
            finalAlbedo.rgb += ((vec3(1) - effectLayerColor * (vec3(1)-tint)));
            break;
            case EFFECT_LAYER_BLEND_MODE_MULT:
            finalAlbedo.rgb *= (effectLayerColor * tint);
            break;
            case EFFECT_LAYER_BLEND_MODE_INV_MULT:
            finalAlbedo.rgb *= ((vec3(1) - effectLayerColor * (vec3(1)-tint)));
            break;
        }

        // pixelRoughness = min(1, pixelRoughness + effectLayerColor.r);
    }

    vec4 finalColor = GetIBLAmbientColor(B_MaterialReceivesLighting,
                                         finalPosition,  finalNormal, finalAlbedo,
                                         pixelRoughness, pixelMetalness);

    float receivesLighting = B_MaterialReceivesLighting ? 0.25 : 0;
    if (receivesLighting > 0 && B_ReceivesShadows)
    {
        receivesLighting = 0.75;
    }

    B_GIn_Albedo = vec4(finalAlbedo.rgb, 1);
    B_GIn_Light = vec4(0,0,0,0);
    B_GIn_Normal = vec4(finalNormal * 0.5f + 0.5f, 0);
    B_GIn_Misc   = vec4(receivesLighting,
                        pixelRoughness,
                        pixelMetalness,
                        0);

    B_GIn_Color = finalColor;
}

void main()
{
    DefaultFragCommonMain(B_MaterialAlbedoColor);
}

